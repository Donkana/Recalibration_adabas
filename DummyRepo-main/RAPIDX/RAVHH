1NEXT L  RAVHH

0310 DEFINE DATA
0320   GLOBAL
0330    USING RAVHGDA
0340   LOCAL
0350    USING RAVHHLDA
0360   LOCAL
0370     1 #DATE-D (D)                /* MUKUNDA 1/21
0380    1 #DOWNLOAD (A1)
0390    1 #RTN-CODE (B4)
0400    1 #PRINT-IND (A1)                         /*SSD992
0410    1 #SECCHK (A19)                       /*RESTRICTED FUNCTION WKAREA
0420    1 REDEFINE #SECCHK                    /*.. ADDED 12/24 - MUKUNDA
0430      2 #SYSID (A1)
0440      2 #USER (A8)
0450      2 #RFI (A8)
0460      2 #RTIND (A2)
0470 **
0480 END-DEFINE
0490 **                                     /* ACTIVATE PF-KEYS
0500 SET KEY PF1 PF2 PF3 PF4 PF5 PF6 PF7 PF8
0510  PF9 PF10 PF11 PF12 PF13 PF14 PF15 PF16
0520  PF17 PF18 PF19 PF20 PF21 PF22 PF23 PF24
0530 **
0540 FORMAT (01) LS=80 PS=60
0550 ***
0560 ASSIGN #RFI = 'RAV'                          /* MUKUNDA 12/24
0570 ASSIGN #SYSID = 'C'                          /* MUKUNDA 12/24
0580 CALLNAT 'AFMB' #SECCHK                       /* MUKUNDA 12/24
0590 IF #RTIND NE 'YY' THEN                       /* MUKUNDA 12/24
0600   STOP                                       /* MUKUNDA 12/24
0610 END-IF /* (0590)                             /* MUKUNDA 12/24
0620 **
0630 ***********************************************************************
0640 ***  DETERMINE LIST OF AWARD DATES AVAILABLE FOR REPORTS
0650 ***********************************************************************
0660 ASSIGN #I = 1
0670 READ CONTROL BY DATE-SEQ-NBR
0680   IF AWARD-DATE (0670) ^= 0            /* CLOSE-DATES NOT FINALIZED
0690     ASSIGN #CLOSE-PROCESSED = 'Y'      /* WILL BE MOST CURRENT RECORDS
0700     #DATE-D := D'2024-12-30' + AWARD-DATE (0670)
0710     MOVE EDITED #DATE-D (EM=DDLLLYY) TO #AWD-DATE (#I)
0720     ASSIGN #AWD-TEXT (#I) = 'AWARD-DATE'
0730     ADD 1 TO #I                                             /* 05/93-DLP
0740     IF #I > 6               /* ALL CLOSE DATE ENTRIES WOULD BE
0750       ESCAPE BOTTOM         /* ..READ PRIOR TO AWARDED ENTRIES, SO
0760     END-IF /* (0740)        /* ..ONCE WE HAVE SIX, CONTINUE PROCESSING
0770   END-IF /* (0680)
0780   IF #CLOSE-PROCESSED = ' '            /* FIND TRUE CLOSE DATE
0790    AND #PARS-CLOSE-DATE = 0
0800     IF IN-PROGRESS (0670) = 'I'        /* DON'T PRODUCE REPORT
0810       WRITE ////////                   /* ..IF AWARD IN PROGRESS
0820         17T 'AWARDS REPORT INHIBITED DURING AWARDS PROCESS'
0830       / 20T 'HIT <ENTER> TO RETURN TO PREVIOUS MENU'
0840       ASSIGN #INHIBIT = 'Y'
0850       ESCAPE BOTTOM
0860     END-IF /* (0800)
0870     IF IN-PROGRESS (0670) = 'R'        /* DON'T PRODUCE RPT   - SSD
0880       WRITE ////////                   /* ..IF DATA BASE BEING BUILT
0890         12T 'AWARDS REPORT UNAVAILABLE - DOCTOR DATA BASE BEING BUILT'
0900       / 20T 'HIT <ENTER> TO RETURN TO PREVIOUS MENU'
0910       ASSIGN #INHIBIT = 'Y'
0920       ESCAPE BOTTOM
0930     END-IF /* (0870)
0940     ASSIGN #PARS-CLOSE-DATE = CLOSE-DATE (0670)
0950     #DATE-D := D'2024-12-30' + #PARS-CLOSE-DATE
0960     MOVE EDITED #DATE-D (EM=DDLLLYY) TO #CLOSE-DATE
0970     ASSIGN #K = 1
0980     FOR #J 1 C*RUN-NUMBER-CROSS-REFERENCE (0670)
0990       HISTOGRAM (1) PROCESSING-HIST FOR DATE-AWD-CAT-TYPE-SEN-NUM-INSERT
1000        STARTING FROM RUN-NBR-PSEUDO-DATE (#J)
1010       END-HISTOGRAM /* (0990)
1020       IF *COUNTER (0990) ^= 0
1030         IF RUN-NBR (0670/#J) ^= 0
1040           ASSIGN #RUN-NBR-N = RUN-NBR (0670/#J)
1050           ASSIGN #RUN-NBR (#K) = #RUN-NBR-A
1060           ASSIGN #RUN-NBR-TEXT (#K) = 'RUN NBR'
1070           ASSIGN #RUN-NBR-PSEUDO-DATE (#K) =
1080            RUN-NBR-PSEUDO-DATE (0670/#J)
1090           ADD 1 TO #K
1100         END-IF /* (1030)
1110       ELSE
1120         ESCAPE BOTTOM
1130       END-IF /* (1020)
1140     END-FOR /* (0980)
1150   END-IF /* (0780)
1160   RESET #CLOSE-PROCESSED
1170 END-READ /* (0670)
1180 IF #INHIBIT = 'Y'
1190   FETCH 'RAVH'
1200 END-IF /* (1180)
1210 **
1220 INPUT
1230  USING MAP 'RAVHHMP1'
1240 **
1250 ******************************************************************
1260 ***                DETERMINE ASSOCIATED TEXT                   ***
1270 ******************************************************************
1280 *** DETERMINE REQUESTED REPORT
1290 FOR #I 1 6
1300   IF #AWD-OPT (#I) ^= ' '
1310     ASSIGN #DATE = #AWD-DATE (#I)
1320     ASSIGN #AWD-CLOSE = 'AWARD'
1330     ASSIGN #AWARDED = 'Y'
1340     MOVE EDITED #AWD-DATE (#I) TO #DATE-D (EM=DDLLLYY)
1350     #AWARDED-DATE := #DATE-D - D'2024-12-30'
1360 **
1370     READ (1) TEXT BY AWARD-DATE
1380      STARTING FROM #AWARDED-DATE
1390       IF #AWARDED-DATE ^= AWARD-DATE (1370)
1400         ESCAPE BOTTOM
1410       END-IF /* (1390)
1420       ASSIGN #SUPER-DATE = CLOSE-DATE (1370)
1430       ASSIGN #TRUE-CLOSE-DATE = CLOSE-DATE (1370)
1440     END-READ /* (1370)
1450     ESCAPE BOTTOM
1460   END-IF /* (1300)
1470 END-FOR /* (1290)
1480 ***
1490 IF #AWARDED ^= 'Y'
1500   FOR #I 1 20
1510     IF #RUN-OPT (#I) ^= ' '
1520       ASSIGN #NBR = #RUN-NBR (#I)
1530       ASSIGN #SUPER-DATE = #RUN-NBR-PSEUDO-DATE (#I)
1540       ASSIGN #DATE = #CLOSE-DATE
1550       ASSIGN #AWD-CLOSE = 'CLOSE'
1560       ASSIGN #TEXT = 'RUN-NBR:'
1570       ASSIGN #TRUE-CLOSE-DATE = #PARS-CLOSE-DATE
1580       ESCAPE BOTTOM
1590     END-IF /* (1510)
1600   END-FOR /* (1500)
1610 END-IF /* (1490)
1620 **
1630 IF #AWARDED = 'Y'
1640   READ (1) CONTROL BY AWARD-DATE       /* FOR AWARD DATE REPORT,
1650    STARTING FROM #AWARDED-DATE         /* ..OBTAIN CONVERSION WINDOW
1660     ACCEPT IF #AWARDED-DATE = AWARD-DATE (1640)
1670     IF FIRST-AE-CONVERSION-DATE (1640) ^= 0
1680       #DATE-D := D'2024-12-30' + FIRST-AE-CONVERSION-DATE (1640)
1690       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #FIRST-DATE
1700       #DATE-D := D'2024-12-30' + LAST-AE-CONVERSION-DATE (1640)
1710       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #LAST-DATE
1720     ELSE                               /* IF NO CONVERSION DATES,
1730       #DATE-D := D'2024-12-30' + FIRST-CANCELLATION-DATE (1640)
1740       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #FIRST-DATE
1750       #DATE-D := D'2024-12-30' + LAST-CANCELLATION-DATE (1640)
1760       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #LAST-DATE
1770     END-IF /* (1670)
1780   END-READ /* (1640)
1790 ELSE
1800   READ (1) CONTROL BY CLOSE-DATE       /* FOR CLOSE DATE RUN REPORT,
1810    STARTING FROM #PARS-CLOSE-DATE      /* ..OBTAIN CONVERSION WINDOW
1820     ACCEPT IF #PARS-CLOSE-DATE = CLOSE-DATE (1800)
1830     IF FIRST-AE-CONVERSION-DATE (1800) ^= 0
1840       #DATE-D := D'2024-12-30' + FIRST-AE-CONVERSION-DATE (1800)
1850       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #FIRST-DATE
1860       #DATE-D := D'2024-12-30' + LAST-AE-CONVERSION-DATE (1800)
1870       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #LAST-DATE
1880     ELSE                               /* IF NO CONVERSION DATES,
1890       #DATE-D := D'2024-12-30' + FIRST-CANCELLATION-DATE (1800)
1900       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #FIRST-DATE
1910       #DATE-D := D'2024-12-30' + LAST-CANCELLATION-DATE (1800)
1920       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #LAST-DATE
1930     END-IF /* (1830)
1940   END-READ /* (1800)
1950 END-IF /* (1630)
1960 ** SSD992 SPLIT MODULE AND ADD MULTIPLE PRINTER CAPABILITY
1970 IF #PRINT-IND = 'Y' THEN
1980   FETCH 'RAVHH1' #SUPER-DATE #PASS                   /* CEM-9/98
1990 ELSE
2000   FETCH 'RAVHH2' #PRNTR #SUPER-DATE #PASS #DOWNLOAD  /* CEM-9/98
2010 END-IF /*(2010)
2020 **
2030 END
***** End of List *****
