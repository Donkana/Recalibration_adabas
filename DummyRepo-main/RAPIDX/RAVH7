1NEXT L  RAVH7

0320 DEFINE DATA LOCAL
0330 **
0340 1 #AWD-IND (A1)
0350 1 #AWARD (A11)                 /* CHG'D FROM 10 BYTES - MUKUNDA 4/94
0360 1 #AWARD-DAY (N5)
0370 1 #AWD-DATE (A7)
0380 1 #CLOSE-DAY (N5)
0390 1 #EMP-NBR (A7)
0400 1 #HARDCOPY (A1)
0410 1 #I (P3)
0420 1 #NAME (A24)
0430 1 #SENIOR (A6)                /* CHG'D FROM 5 BYTES - MUKUNDA 4/94
0440 1 #TODAY (N5)
0450 1 #TYPE-WK (A1)                                  /* 4/92-RAV
0460 **
0470 1 #CAT-SAVE (A7)                                 /* 4/92-
0480 1 #DATE-SAVE (N5)                                /* RAV
0490 1 #TYPE-SAVE (A1)                                /*
0500 **
0510 1 #CATEGORY (A7/5)                               /* 4/92-
0520 1 #DATE (N5/5)                                   /* RAV
0530 1 #TYPE (A1/5)                                   /*
0540 **
0550 1 #DATE-D (D)
0560 **
0570 1 PERSONNEL VIEW OF DOCTOR-PERSONNEL-DATA
0580   2 SENIORITY-NUM
0590   2 SENIORITY-INSERTION-CHAR
0600   2 EMPLOYEE-NBR
0610   2 LAST-NAME
0620   2 FIRST-NAME
0630   2 REDEFINE FIRST-NAME
0640     3 #1ST-INIT (A1)
0650   2 MIDDLE-NAME
0660   2 REDEFINE MIDDLE-NAME
0670     3 #2ND-INIT (A1)
0680 **
0690 1 PROCESS VIEW OF DOCTOR-VACANCY-PROCESS-CONTROL
0700   2 CLOSE-DATE
0710   2 AWARD-DATE
0720   2 DATE-SEQ-NBR
0730   2 FINAL-RUN-INDICATOR
0740 **
0750 1 AWARD-HIST VIEW OF DOCTOR-VACANCY-AWARD-PROCESSING
0760   2 DATE-SENIOR-NUM-INSERT
0770 **
0780 1 AWARD VIEW OF DOCTOR-VACANCY-AWARD-PROCESSING
0790   2 SENIORITY-NUM
0800   2 SENIORITY-INSERTION-CHAR
0810   2 EMPLOYEE-NBR
0820   2 CLOSE-DATE
0830   2 TRANSFER-TYPE-IND
0840   2 AWARDED-CATEGORY
0850   2 CONVERSION-DATE
0860   2 OUTSTANDING-AWARD-CAT (2)                    /* OUTSTANDING AWARD
0870   2 OUTSTANDING-AWARD-DATE (2)                   /* FIELDS ADDED
0880   2 OUTSTANDING-CONVER-DATE (2)                  /* 4/92 - RAV
0890   2 OUT-D/R-CAT (3)
0900   2 OUT-D/R-AWD-DATE (3)
0910   2 OUT-D/R-CONV-DATE (3)
0920   2 OUT-D/R-TYPE (3)
0930 **
0940 END-DEFINE /* (0320)
0950 **
0960 SET KEY PF1 = 'MAIN'
0970 SET KEY PF2 = 'RAVH'
0980 **
0990 #TODAY := *DATX - D'2024-12-30'
1000 **
1010 INPUT USING MAP 'RAVH7MAP'
1020 **
1030 ***************************
1040 ** EMPLOYEE NUMBER AUDIT **
1050 ***************************
1060 **
1070 FIND (1) PERSONNEL WITH EMPLOYEE-NBR = #EMP-NBR
1080   COMPRESS LAST-NAME #1ST-INIT #2ND-INIT INTO
1090     #NAME WITH DELIMITER ' '
1100   COMPRESS SENIORITY-NUM SENIORITY-INSERTION-CHAR
1110     INTO #SENIOR LEAVING NO
1120 END-FIND /* (1070)
1130 IF *COUNTER (1070) EQ 0
1140   THEN
1150   REINPUT 'INPUT EMPLOYEE NBR NOT FOUND IN PERSONNEL FILE'
1160     MARK 1 SOUND ALARM
1170 END-IF /* (1130)
1180 **
1190 *************************************
1200 ** DETERMINE IF AWARD RECORD FOR   **
1210 ** PREVIOUS AWARD EXISTS FOR DOCTOR **
1220 *************************************
1230 **
1240 READ PROCESS BY DATE-SEQ-NBR
1250   IF FINAL-RUN-INDICATOR (1240) NE 'F'
1260     THEN
1270     ESCAPE TOP
1280   END-IF /* (1250)
1290   ASSIGN #CLOSE-DAY = CLOSE-DATE (1240)
1300   ASSIGN #AWARD-DAY = AWARD-DATE (1240)
1310   ESCAPE BOTTOM
1320 END-READ
1330 **
1340 COMPRESS #CLOSE-DAY #SENIOR INTO
1350   #AWARD LEAVING NO
1360 HISTOGRAM AWARD-HIST DATE-SENIOR-NUM-INSERT
1370   STARTING FROM #AWARD THRU #AWARD
1380 END-HISTOGRAM /* (1360)
1390 IF *COUNTER (1360) EQ 0 THEN
1400   REINPUT 'NO AWARD PROCESSING RECORD FOUND FOR DOCTOR'
1410     MARK *#EMP-NBR SOUND ALARM
1420 END-IF /* (1390)
1430 **
1440 *********************************************************
1450 ** READ AWARD RECORD AND MOVE EXISTING UNCONVERTED     **
1460 ** AWARD INFORMATION INTO A WORK ARRAY, SORTING AWARDS **
1470 ** IN DESCENDING CHRONOLOGICAL ORDER BY AWARD DATE.    **
1480 *********************************************************
1490 **
1500 FIND (1) AWARD WITH DATE-SENIOR-NUM-INSERT = #AWARD
1510   IF AWARDED-CATEGORY (1500) EQ ' ' AND
1520       OUT-D/R-CAT (1) EQ ' ' AND
1530       OUT-D/R-CAT (2) EQ ' ' AND
1540     OUTSTANDING-AWARD-CAT (1) = ' ' AND
1550     OUTSTANDING-AWARD-CAT (2) = ' '
1560     THEN
1570     ESCAPE BOTTOM
1580   END-IF /* (1550)
1590   ASSIGN #AWD-IND = 'Y'
1600   DECIDE FOR EVERY CONDITION
1610     WHEN AWARDED-CATEGORY NE ' ' AND
1620         (CONVERSION-DATE = 0 OR
1630         CONVERSION-DATE GT #TODAY)
1640       ADD 1 TO #I
1650       MOVE AWARDED-CATEGORY TO #CATEGORY (#I)
1660       MOVE #AWARD-DAY TO #DATE (#I)
1670       ASSIGN #TYPE-WK = TRANSFER-TYPE-IND
1680       PERFORM TYPE
1690     WHEN OUT-D/R-CAT (3) NE ' ' AND                     /* MUKUNDA 2/94
1700         (OUT-D/R-CONV-DATE (3) = 0 OR                   /* MUKUNDA 2/94
1710         OUT-D/R-CONV-DATE (3) GT #TODAY)                /* MUKUNDA 2/94
1720       ADD 1 TO #I                                       /* MUKUNDA 2/94
1730       MOVE OUT-D/R-CAT (3) TO #CATEGORY (#I)            /* MUKUNDA 2/94
1740       MOVE OUT-D/R-AWD-DATE (3) TO #DATE (#I)           /* MUKUNDA 2/94
1750       ASSIGN #TYPE-WK = OUT-D/R-TYPE (3)                /* MUKUNDA 2/94
1760       PERFORM TYPE                                      /* MUKUNDA 2/94
1770     WHEN OUT-D/R-CAT (2) NE ' ' AND
1780         (OUT-D/R-CONV-DATE (2) = 0 OR
1790         OUT-D/R-CONV-DATE (2) GT #TODAY)
1800       ADD 1 TO #I
1810       MOVE OUT-D/R-CAT (2) TO #CATEGORY (#I)
1820       MOVE OUT-D/R-AWD-DATE (2) TO #DATE (#I)
1830       ASSIGN #TYPE-WK = OUT-D/R-TYPE (2)
1840       PERFORM TYPE
1850     WHEN OUTSTANDING-AWARD-CAT (2) NE ' ' AND
1860         (OUTSTANDING-CONVER-DATE (2) = 0 OR
1870         OUTSTANDING-CONVER-DATE (2) GT #TODAY)
1880       IF #I = 0
1890         THEN
1900         ADD 1 TO #I
1910       END-IF /* (1880)
1920       IF OUTSTANDING-AWARD-DATE (2) GT #DATE (#I)
1930         THEN
1940         MOVE #CATEGORY (#I) TO #CAT-SAVE
1950         MOVE #DATE (#I) TO #DATE-SAVE
1960         MOVE #TYPE (#I) TO #TYPE-SAVE
1970         ASSIGN #CATEGORY (#I) = OUTSTANDING-AWARD-CAT (2)
1980         ASSIGN #DATE (#I) = OUTSTANDING-AWARD-DATE (2)
1990         ASSIGN #TYPE (#I) = 'A'
2000         ADD 1 TO #I
2010         ASSIGN #CATEGORY (#I) = #CAT-SAVE
2020         ASSIGN #DATE (#I) = #DATE-SAVE
2030         ASSIGN #TYPE (#I) = #TYPE-SAVE
2040       ELSE
2050         ADD 1 TO #I
2060         MOVE OUTSTANDING-AWARD-CAT (2) TO #CATEGORY (#I)
2070         MOVE OUTSTANDING-AWARD-DATE (2) TO #DATE (#I)
2080         ASSIGN #TYPE (#I) = 'A'
2090       END-IF /* (1920)
2100     WHEN OUT-D/R-CAT (1) NE ' ' AND
2110         (OUT-D/R-CONV-DATE (1) = 0 OR
2120         OUT-D/R-CONV-DATE (1) GT #TODAY)
2130       IF #I = 0
2140         THEN
2150         ADD 1 TO #I
2160       END-IF /* (2130)
2170       IF OUT-D/R-AWD-DATE (1) GT #DATE (#I)
2180         THEN
2190         MOVE #CATEGORY (#I) TO #CAT-SAVE
2200         MOVE #DATE (#I) TO #DATE-SAVE
2210         MOVE #TYPE (#I) TO #TYPE-SAVE
2220         ASSIGN #CATEGORY (#I) = OUT-D/R-CAT (1)
2230         ASSIGN #DATE (#I) = OUT-D/R-AWD-DATE (1)
2240         ASSIGN #TYPE-WK = OUT-D/R-TYPE (1)
2250         PERFORM TYPE
2260         ADD 1 TO #I
2270         MOVE #CAT-SAVE TO #CATEGORY (#I)
2280         MOVE #DATE-SAVE TO #DATE (#I)
2290         MOVE #TYPE-SAVE TO #TYPE (#I)
2300       ELSE
2310         ADD 1 TO #I
2320         MOVE OUT-D/R-CAT (1) TO #CATEGORY (#I)
2330         MOVE OUT-D/R-AWD-DATE (1) TO #DATE (#I)
2340         ASSIGN #TYPE-WK = OUT-D/R-TYPE (1)
2350         PERFORM TYPE
2360       END-IF /* (2170)
2370     WHEN OUTSTANDING-AWARD-CAT (1) NE ' ' AND
2380         (OUTSTANDING-CONVER-DATE (1) = 0 OR
2390         OUTSTANDING-CONVER-DATE (1) GT #TODAY)
2400       IF #I = 0
2410         THEN
2420         ADD 1 TO #I
2430       END-IF /* (2400)
2440       IF OUTSTANDING-AWARD-DATE (1) GT #DATE (#I)
2450         THEN
2460         MOVE #CATEGORY (#I) TO #CAT-SAVE
2470         MOVE #DATE (#I) TO #DATE-SAVE
2480         MOVE #TYPE (#I) TO #TYPE-SAVE
2490         ASSIGN #CATEGORY (#I) = OUTSTANDING-AWARD-CAT (1)
2500         ASSIGN #DATE (#I) = OUTSTANDING-AWARD-DATE (1)
2510         ASSIGN #TYPE (#I) = 'A'
2520         ADD 1 TO #I
2530         MOVE #CAT-SAVE TO #CATEGORY (#I)
2540         MOVE #DATE-SAVE TO #DATE (#I)
2550         MOVE #TYPE-SAVE TO #TYPE (#I)
2560         IF #I GT 2 THEN                                    /* MUKUNDA 11/93
2570           IF #DATE (#I - 1) GT #DATE (#I - 2) THEN         /* MUKUNDA 11/93
2580             MOVE #CATEGORY(#I - 2) TO #CAT-SAVE            /* MUKUNDA 11/93
2590             MOVE #DATE(#I - 2) TO #DATE-SAVE               /* MUKUNDA 11/93
2600             MOVE #TYPE(#I - 2) TO #TYPE-SAVE               /* MUKUNDA 11/93
2610             ASSIGN #CATEGORY(#I - 2) = #CATEGORY(#I - 1)   /* MUKUNDA 11/93
2620             ASSIGN #DATE(#I - 2) = #DATE(#I - 1)           /* MUKUNDA 11/93
2630             ASSIGN #TYPE(#I - 2) = #TYPE(#I - 1)           /* MUKUNDA 11/93
2640             MOVE #CAT-SAVE TO #CATEGORY(#I - 1)            /* MUKUNDA 11/93
2650             MOVE #DATE-SAVE TO #DATE(#I - 1)               /* MUKUNDA 11/93
2660             MOVE #TYPE-SAVE TO #TYPE(#I - 1)               /* MUKUNDA 11/93
2670           END-IF /* (2570)                                 /* MUKUNDA 11/93
2680         END-IF /* (2560)                                   /* MUKUNDA 11/93
2690       ELSE
2700         ADD 1 TO #I
2710         MOVE OUTSTANDING-AWARD-CAT (1) TO #CATEGORY (#I)
2720         MOVE OUTSTANDING-AWARD-DATE (1) TO #DATE (#I)
2730         ASSIGN #TYPE (#I) = 'A'
2740       END-IF /* (2440)
2750     WHEN NONE
2760       IGNORE
2770   END-DECIDE /* (1600)
2780 **
2790   IF #HARDCOPY EQ 'H'
2800     THEN
2810     WRITE (01) 18T '=*= DOCTOR OUTSTANDING AWARDS =*=' /
2820       7T 'NAME:' #NAME 5X 'EMPLOYEE NBR:' #EMP-NBR /
2830 '--------------------------------------------------------------------'
2840       /  7T 'A = ADVANCE ENTITLEMENT       D = DISPLACEMENT'
2850       /  7T 'R = REINSTATEMENT             V = VOLUNTARY DISPLACEMENT'
2860       // 16T 'AWARD DATE' 5X 'AWARD TYPE' 5X 'CATEGORY' /
2870       16T '----------' 5X '----------' 5X '--------' /
2880   ELSE
2890     WRITE 18T '=*= DOCTOR OUTSTANDING AWARDS =*=' /
2900       7T 'NAME:' #NAME 5X 'EMPLOYEE NBR:' #EMP-NBR /
2910 '--------------------------------------------------------------------'
2920       /  7T 'A = ADVANCE ENTITLEMENT       D = DISPLACEMENT'
2930       /  7T 'R = REINSTATEMENT             V = VOLUNTARY DISPLACEMENT'
2940       // 16T 'AWARD DATE' 5X 'AWARD TYPE' 5X 'CATEGORY' /
2950       16T '----------' 5X '----------' 5X '--------' /
2960   END-IF /* (2790)
2970 **
2980   FOR #I = 1 TO 5
2990     IF #DATE (#I) NE 0
3000       THEN
3010       #DATE-D := #DATE (#I) + D'2024-12-30'
3020       MOVE EDITED #DATE-D(EM=DDLLLYY) TO #AWD-DATE
3030     ELSE
3040       ASSIGN #AWD-DATE = ' '
3050     END-IF /* (2990)
3060     IF #HARDCOPY EQ 'H'
3070       THEN
3080       WRITE (01) 17T #AWD-DATE 11X #TYPE (#I) 10X #CATEGORY (#I)
3090     ELSE
3100       WRITE 17T #AWD-DATE 11X #TYPE (#I) 10X #CATEGORY (#I)
3110     END-IF /* (3060)
3120   END-FOR /* (2980)
3130 **
3140 END-FIND /* (1500)
3150 **
3160 IF #AWD-IND = ' '
3170   THEN
3180   WRITE //////
3190     17T 'NO OUTSTANDING AWARDS FOR SPECIFIED DOCTOR EXIST'
3200     ////////////// 30T 'HIT ENTER TO CONTINUE'
3210 ELSE
3220   IF #HARDCOPY NE 'H'
3230     THEN
3240     WRITE // 16T 'END OF DISPLAY -- HIT ENTER TO CONTINUE'
3250   ELSE
3260     WRITE (01) // 29T 'END OF REPORT'
3270     WRITE //////
3280       13T 'HARDCOPY OF DOCTOR OUTSTANDING AWARDS ROUTED TO PRINTER'
3290       ////////////// 30T 'HIT ENTER TO CONTINUE'
3300   END-IF /* (3220)
3310 END-IF /* (3160)
3320 DEFINE PRINTER (01)
3330 **
3340 RELEASE VARIABLES
3350 FETCH 'RAVH7'
3360 **
3370 *****************
3380 ** SUBROUTINES **
3390 *****************
3400 **
3410 DEFINE SUBROUTINE TYPE
3420 DECIDE FOR FIRST CONDITION
3430   WHEN #TYPE-WK = 'O'
3440     ASSIGN #TYPE (#I) = 'A'
3450   WHEN #TYPE-WK = 'A'
3460     ASSIGN #TYPE (#I) = 'D'
3470   WHEN #TYPE-WK = 'R'
3480     ASSIGN #TYPE (#I) = 'R'
3490   WHEN #TYPE-WK = 'H'
3500     ASSIGN #TYPE (#I) = 'V'
3510   WHEN NONE
3520     IGNORE
3530 END-DECIDE /* (3530)
3540 END-SUBROUTINE
3550 **
3560 END
***** End of List *****
