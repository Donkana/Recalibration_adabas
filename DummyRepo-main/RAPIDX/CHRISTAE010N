1NEXT L  CHRISTAE010N

0470 DEFINE DATA
0480 PARAMETER USING CHRISTAE001A                     /* Parameter data area
0490 LOCAL
0500 1 #CALL (I2)
0510 1 #CLASS (A1)
0520 1 #LENGTH (B2) INIT <8400>
0530 1 #MSGCLASS (A1)
0540 1 #RTN-CODE (B4)
0550 1 #STATUS (A4)
0560 1 #SYS-CHAR (A1)
0570 1 #SYSTEM (A2)
0580 1 REDEFINE #SYSTEM
0590   02 #SYS (A1)
0600   02 #RTN (A1)
0610 1 #SYSOUT (A1)
0620 1 #TIME (B2) INIT <1>
0630 **
0640 1 #JCL-STATEMENTS
0650   2 #CARD (A80/105) INIT
0660     <'//&JOBNAME JOB ,''TSS'','
0670     ,'//            CLASS=%,'
0680     ,'//            MSGCLASS=$'
0690     ,'//STEPA  EXEC CSBATCH,'
0700     ,'//            PARM=''PRINTER=(&PRNT)'','
0710     ,'//            SYSOUT=@,'
0720     ,'//            STATUS=!'
0730     ,'//SYSPRINT DD SYSOUT=@'
0740     ,'//CMWKF01  DD DSN=&&CHRISTAE001P,'
0750     ,'//            DISP=(NEW,PASS,DELETE),'
0760     ,'//            UNIT=SYSDA,'
0770     ,'//            DCB=(RECFM=FB),'
0780     ,'//            SPACE=(CYL,(2,1),RLSE)'
0790     ,'//SYSIN    DD *'
0800     ,'RAPIDX,&USER-ID'
0810     ,'&PASSWRD'
0820     ,'CHRISTAE001P'
0830     ,'&TRNG-PARS'
0840     ,'&PSEUDO-PARS'
0850     ,'FIN'
0860     ,'/*'
0870     ,'//*'
0880     ,'//* ==========================================================='
0890     ,'//* STEPB Sorts data based on sort-key generated by STEPA'
0900     ,'//* ==========================================================='
0910     ,'//STEPB  EXEC PGM=ICEMAN'
0920     ,'//SYSOUT   DD SYSOUT=@'
0930     ,'//SORTIN   DD UNIT=SYSDA,'
0940     ,'//            DISP=(OLD,PASS,DELETE),'
0950     ,'//            DSN=&&CHRISTAE001P,'
0960     ,'//            DCB=(RECFM=FB)'
0970     ,'//SORTOUT  DD UNIT=SYSDA,'
0980     ,'//            DISP=(NEW,PASS,DELETE),'
0990     ,'//            DSN=&&CHRISTAE002P,'
1000     ,'//            DCB=(RECFM=FB),'
1010     ,'//            SPACE=(TRK,(30,8),RLSE)'
1020     ,'//SORTWK01 DD UNIT=SYSDA,'
1030     ,'//            SPACE=(CYL,(2,1))'
1040     ,'//SYSIN    DD *'
1050     ,' SORT FIELDS=(77,18,A),FORMAT=CH'
1060     ,' END'
1070     ,'//* ==========================================================='
1080     ,'//* STEPC Builds print files, headers and prints'
1090     ,'//* ==========================================================='
1100     ,'//STEPC  EXEC CSBATCH,'
1110     ,'//            PARM=''PRINTER=(&PRNT)'','
1120     ,'//            STATUS=!,'
1130     ,'//            SYSOUT=@'
1140     ,'//SYSPRINT DD SYSOUT=*'
1150     ,'//CMWKF01  DD DSN=&&CHRISTAE002P,'
1160     ,'//            DISP=(OLD,PASS,KEEP),'
1170     ,'//            DCB=(RECFM=FB),'
1180     ,'//            UNIT=SYSDA'
1190     ,'//SYSIN    DD *'
1200     ,'RAPIDX,&USER-ID'
1210     ,'&PASSWRD'
1220     ,'CHRISTAE002P'
1230     ,'&TRNG-PARS'
1240     ,'&END-PARS'
1250     ,'&CLOSE-PARS'
1260     ,'&WIN-BEG-PARS'
1270     ,'&WIN-END-PARS'
1280     ,'&RUN-NBR'
1290     ,'FIN'
1300     ,'/*'
1310     ,'//*'
1320     ,'//* ==========================================================='
1330     ,'//* STEPD Sorts data based on training category equip-pos'
1340     ,'//* ==========================================================='
1350     ,'//STEPD  EXEC PGM=ICEMAN'
1360     ,'//SYSOUT   DD SYSOUT=@'
1370     ,'//SORTIN   DD UNIT=SYSDA,'
1380     ,'//            DISP=(OLD,PASS,DELETE),'
1390     ,'//            DSN=&&CHRISTAE001P,'
1400     ,'//            DCB=(RECFM=FB)'
1410     ,'//SORTOUT  DD UNIT=SYSDA,'
1420     ,'//            DISP=(NEW,PASS,DELETE),'
1430     ,'//            DSN=&&CHRISTAE003P,'
1440     ,'//            DCB=(RECFM=FB),'
1450     ,'//            SPACE=(TRK,(30,8),RLSE)'
1460     ,'//SORTWK01 DD UNIT=SYSDA,'
1470     ,'//            SPACE=(CYL,(2,1))'
1480     ,'//SYSIN    DD *'
1490     ,' SORT FIELDS=(85,4,A),FORMAT=CH'
1500     ,' END'
1510     ,'//* ==========================================================='
1520     ,'//* STEPE Builds numbers array, headers and prints'
1530     ,'//* ==========================================================='
1540     ,'//STEPE  EXEC CSBATCH,'
1550     ,'//            PARM=''PRINTER=(&PRNT)'','
1560     ,'//            STATUS=!,'
1570     ,'//            SYSOUT=@'
1580     ,'//SYSPRINT DD SYSOUT=*'
1590     ,'//CMWKF01  DD DSN=&&CHRISTAE003P,'
1600     ,'//            DISP=(OLD,PASS,KEEP),'
1610     ,'//            DCB=(RECFM=FB),'
1620     ,'//            UNIT=SYSDA'
1630     ,'//SYSIN    DD *'
1640     ,'RAPIDX,&USER-ID'
1650     ,'&PASSWRD'
1660     ,'CHRISTAE003P'
1670     ,'FIN'
1680     ,'/*'
1690     ,'//*'
1700     ,'//'>
1710 END-DEFINE
1720 * =================================================================== *
1730 *                          Set JCL Parameters                         *
1740 * =================================================================== *
1750 CALL 'WHERE' #SYSTEM                   /* Initiate "WHERE" utility to
1760 **                                     /* ..determine on which system
1770 IF #RTN NE '0'                         /* ..THIS PROGRAM IS BEING
1780   ASSIGN CHRISTAE001A.ERROR-CODE = 800     /* ..executed
1790   ESCAPE ROUTINE
1800 END-IF /* (1770)
1810 **
1820 DECIDE ON FIRST VALUE OF #SYS          /* Depending on system in which
1830   VALUE 'P'                            /* ..program resides, set values
1840     ASSIGN #STATUS = 'PROD'            /* ..for JCL as PRODUCTION,
1850     ASSIGN #SYS-CHAR = 'U'
1860     ASSIGN #CLASS = 'J'
1870     ASSIGN #MSGCLASS = 'Q'             /* ..(("X" HOLDS UQ INFORMATION
1880     ASSIGN #SYSOUT = 'Q'               /* ..ON QUEUE FOR EVALUATION)
1890   VALUE 'A'
1900     ASSIGN #STATUS = 'ACCP'            /* ..ACCEPTANCE,
1910     ASSIGN #SYS-CHAR = 'A'
1920     ASSIGN #CLASS = 'Z'
1930     ASSIGN #MSGCLASS = 'X'             /* ..(("X" holds UQ information
1940     ASSIGN #SYSOUT = 'X'               /* ..on queue for evaluation)
1950   VALUE 'T'
1960     ASSIGN #STATUS = 'TEST'            /* ..or TEST as appropriate
1970     ASSIGN #SYS-CHAR = 'T'
1980     ASSIGN #CLASS = 'Z'
1990     ASSIGN #MSGCLASS = 'X'             /* ..(("X" holds UQ information
2000     ASSIGN #SYSOUT = 'X'               /* ..on queue for evaluation)
2010   NONE
2020     IGNORE
2030 END-DECIDE /* (1820)
2040 * ==================================================================== *
2050 *    Set the Defaults for Blank Fields and Enter Appropriate Values    *
2060 * ==================================================================== *
2070 EXAMINE #CARD (*) '%' REPLACE WITH #CLASS
2080 EXAMINE #CARD (*) '$' REPLACE WITH #MSGCLASS
2090 **
2100 COMPRESS #SYS-CHAR 'TG026' 'M' INTO JOBNAME LEAVING NO
2110 **
2120 EXAMINE FULL #CARD (1) '&JOBNAME' REPLACE WITH JOBNAME
2130 EXAMINE FULL #CARD (*) '&USER-ID' REPLACE WITH 'TG026'
2140 EXAMINE FULL #CARD (*) '&PASSWRD' REPLACE WITH 'TG026'
2150 EXAMINE #CARD (*) '!' REPLACE WITH #STATUS
2160 EXAMINE #CARD (*) '&PRNT' REPLACE WITH CHRISTAE001A.PRINTER
2170 EXAMINE #CARD (*) '@' REPLACE WITH #SYSOUT
2180 EXAMINE #CARD (*) '&TRNG-PARS' REPLACE WITH CHRISTAE001A.TRNG-ALPHA
2190 EXAMINE #CARD (*) '&PSEUDO-PARS' REPLACE WITH CHRISTAE001A.PSEUDO-ALPHA
2200 EXAMINE #CARD (*) '&END-PARS' REPLACE WITH CHRISTAE001A.END-ALPHA
2210 EXAMINE #CARD (*) '&CLOSE-PARS' REPLACE WITH CHRISTAE001A.CLOSE-ALPHA
2220 EXAMINE #CARD (*) '&WIN-BEG-PARS' REPLACE WITH CHRISTAE001A.WIN-BEG-ALPHA
2230 EXAMINE #CARD (*) '&WIN-END-PARS' REPLACE WITH CHRISTAE001A.WIN-END-ALPHA
2240 IF CHRISTAE001A.#DETAIL-REPORT-IND NE 'Y'   /* DON'T PRODUCE DETAIL REPORT
2250   CHRISTAE001A.RUN-ALPHA := '99'            /* (CHRISTAE002P)
2260 END-IF /* (2240)
2270 EXAMINE FULL #CARD (*) '&RUN-NBR' REPLACE WITH CHRISTAE001A.RUN-ALPHA
2280 **
2290 * ==================================================================== *
2300 *                  Call 'RJE' - Submit the Batch Job                   *
2310 * ==================================================================== *
2320 FOR #CALL 1 10                         /* Begin RJE utility
2330   CALL 'RJE' #RTN-CODE #CARD (1) #LENGTH
2340 **
2350   IF #RTN-CODE = 0
2360     ESCAPE ROUTINE
2370   END-IF /* (2340)
2380   IF #RTN-CODE = 4
2390     ASSIGN #TIME = 1
2400     CALL 'ROLBCH' #TIME
2410   END-IF /* (2380)
2420 END-FOR /* (2320)
2430 **
2440 IF #RTN-CODE = 4
2450   ASSIGN CHRISTAE001A.ERROR-CODE = 900
2460   ESCAPE ROUTINE
2470 END-IF /* (2440)
2480 **
2490 END
***** End of List *****
