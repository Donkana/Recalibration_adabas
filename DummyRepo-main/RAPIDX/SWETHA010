1NEXT L  MUKUNDASMH
0130 DEFINE DATA LOCAL
0150 1 #PROG (A8)                RAV 1/00
0160 1 #MATRIX-TYPE (A1)         RAV 1/00
0170 1 #AWD-DATE (A7)
0180 1 REDEFINE #AWD-DATE
0190   2 #AWD-DAY (A2)
0200   2 #AWD-MTH (A3)
0210   2 #AWD-YR (A2)
0220 1 #AWD-PARS (N5)
0230 1 #CLOSE-DATE (A7)
0240 1 REDEFINE #CLOSE-DATE
0250   2 #CLOSE-DD (A2)
0260   2 #CLOSE-MMM (A3)
0270   2 #CLOSE-YY (A2)
0280 1 #CLOSE-PARS (N5)
0290 1 #DATE (A7)
0300 1 #DEST (A4)
0310 1 REDEFINE #DEST
0320   2 #DEST-N (N4)
0330 1 REDEFINE #DEST
0340   2 #DEST-SV (A3)
0350 1 #I (N3)
0360 1 #RUN-NBR (A2)
0370 1 REDEFINE #RUN-NBR
0380   2 #RUN-NBR-NUM (N2)
0390 1 #SUPER-CLOSE (N5)
0400 1 #TYPE (A1)                                     /* 2/92-RAV
0410 **
0420 1 #L1 (A80)                                      /* JCL FIELDS
0430 1 #L2 (A80)
0440 1 REDEFINE #L2
0450   2 #FILLER (A38)
0460   2 #PRINTER (A4)
0470 1 #L3 (A80)
0480 1 #L4 (A80)
0490 1 #L5 (A80)
0500 1 #L6 (A80)
0510 1 #L7 (A80)
0520 1 #LENGTH (B2)
0530 **
0540 1 #W (A1)                                        /* 'WHERE' LOGIC
0550 1 #RC (A1)                                       /* FIELDS
0560 1 #L-CLASS (A1)
0570 1 #G-CLASS (A1)
0580 1 #MSG-CLS (A1)
0590 1 #SYSOUT (A1)
0600 1 #STATUS (A4)
0610 **
0620 1 #D (N2)                                        /* 'RJE' FIELDS
0630 1 #RTN-CODE (B4)
0640 1 #TIME (B2)
0650 **
0660 1 #GETCHR-FIELD (A16)                            /* PRINTER CHECK
0670 1 REDEFINE #GETCHR-FIELD                         /* UTILITY AREA
0680   2 #GETCHR-TID (B2)
0690   2 FILL6 (A4)
0700   2 #MAX-NBR-LINES (B2)
0710 1 #GETCHR-RET-CODE (B4)
0720 **
0730 1 #WORK (A80)
0740 1 REDEFINE #WORK
0750   2 #ACTION (A3)
0760   2 #RET-CODE (A1)
0770   2 #NUM-DAY (A2)
0780   2 #ALPHA-MTH (A3)
0790   2 #NUM-YR (A2)
0800   2 #FILL1 (A4)
0810   2 #NUM-MTH (A2)
0820   2 #FILL2 (A2)
0830   2 #PARS (N5)
0840   2 #REMAIN (A56)
0850 **
0860 1 CONTROL-HIST1 VIEW OF DOCTOR-VACANCY-PROCESS-CONTROL
0870   2 AWARD-DATE
0880 **
0890 1 CONTROL-HIST2 VIEW OF DOCTOR-VACANCY-PROCESS-CONTROL
0900   2 CLOSE-DATE
0910 **
0920 1 CONTROL VIEW OF DOCTOR-VACANCY-PROCESS-CONTROL
0930   2 AWARD-DATE
0940   2 CLOSE-DATE
0950   2 C*RUN-NUMBER-CROSS-REFERENCE
0960   2 RUN-NUMBER-CROSS-REFERENCE
0970     3 RUN-NBR (1:99)
0980     3 RUN-NBR-PSEUDO-DATE (1:99)
0990 **
1000 1 #SECCHK (A19)                       /*RESTRICTED FUNCTION WKAREA
1010 1 REDEFINE #SECCHK                    /*.. ADDED 12/24 - MUKUNDA
1020   2 #SYSID (A1)
1030   2 #USER (A8)
1040   2 #RFI (A8)
1050   2 #RTIND (A2)
1060 END-DEFINE /* (0010)
1070 **
1080 SET KEY PF1 PF2
1090 **
1100 ASSIGN #RFI = 'RAV'                          /* MUKUNDA 12/24
1110 ASSIGN #SYSID = 'C'                          /* MUKUNDA 12/24
1120 CALLNAT 'AFMB' #SECCHK                       /* MUKUNDA 12/24
1130 IF #RTIND NE 'YY' THEN                       /* MUKUNDA 12/24
1140   STOP                                       /* MUKUNDA 12/24
1150 END-IF /* (1130)                             /* MUKUNDA 12/24
1160 **
1170 INPUT 70T '(UR6877)' / 70T '(MUKUNDASMH)' //
1180  21T '** DOCTOR OPERATIONAL MATRIX REPORT **' ///
1190  21T 'ENTER THE FOLLOWING:' //
1200  21T 'MATRIX TYPE (C=CATEGORY,E=EQP/POS):' #MATRIX-TYPE (AD=AI'_') //
1210  21T 'REPORT TYPE (A=ADV.ENT.,D=DISP.,C=COMPLETE):' #TYPE (AD=AI'_') //
1220  21T 'AWARD DATE (DDMMMYY):' #AWD-DATE (AD=AI'_') /
1230  32T '*OR*' /
1240  21T 'CASE CLOSE DATE (DDMMMYY)/RUN NBR:'
1250  #CLOSE-DATE (AD=AI'_') '/' #RUN-NBR (AD=AI'_') //
1260  21T 'PRINTER ADDRESS:' #DEST (AD=AI'_') //
1270  /// 17T 'HIT PF1 FOR MAIN MENU OR PF2 FOR PREVIOUS MENU'
1280 **
1290 IF *PF-KEY = 'PF1'
1300   THEN
1310   FETCH 'MAIN'
1320 END-IF /* (1290)
1330 IF *PF-KEY = 'PF2'
1340   THEN
1350   FETCH 'RAVH'
1360 END-IF /* (1330)
1370 **
1380 RESET #AWD-PARS #CLOSE-PARS
1390 **
1400 IF #TYPE NE 'A' AND #TYPE NE 'D' AND             /* 2/92-RAV
1410   #TYPE NE 'C'                                   /*
1420   THEN                                           /*
1430   REINPUT 'REPORT TYPE MUST BE "A", "D" OR "C"'  /*
1440     MARK *#TYPE SOUND ALARM                      /*
1450 END-IF /* (1400)                                 /*
1460 **
1470 IF #AWD-DATE NE ' '
1480   THEN
1490   IF #CLOSE-DATE NE ' ' OR #RUN-NBR NE ' '
1500     THEN
1510     REINPUT 'PLEASE SELECT EITHER AWARD DATE OR CASE CLOSE DATE/RUN NBR'
1520       MARK *#AWD-DATE SOUND ALARM
1530   END-IF /* (1490)
1540   ASSIGN #DATE = #AWD-DATE
1550   ASSIGN #NUM-DAY = #AWD-DAY
1560   ASSIGN #ALPHA-MTH = #AWD-MTH
1570   ASSIGN #NUM-YR = #AWD-YR
1580   ASSIGN #ACTION = 'CAL'
1590   PERFORM CONVERT
1600   ASSIGN #AWD-PARS = #PARS
1610 ELSE
1620   IF #CLOSE-DATE = ' ' AND #RUN-NBR = ' '
1630     THEN
1640     REINPUT 'PLEASE SELECT EITHER AWARD DATE OR CASE CLOSE DATE/RUN NBR'
1650       MARK *#AWD-DATE SOUND ALARM
1660   END-IF /* (1620)
1670 END-IF /* (1470)
1680 **
1690 IF #CLOSE-DATE NE ' '
1700   THEN
1710   IF #RUN-NBR = ' '
1720     THEN
1730     REINPUT 'RUN NUMBER REQUIRED'
1740       MARK *#RUN-NBR SOUND ALARM
1750   ELSE
1760     IF #RUN-NBR NE MASK (9)
1770       THEN
1780       REINPUT 'INVALID RUN NUMBER'
1790         MARK *#RUN-NBR SOUND ALARM
1800     END-IF /* (1760)
1810   END-IF /* (1710)
1820   ASSIGN #DATE = #CLOSE-DATE
1830   ASSIGN #NUM-DAY = #CLOSE-DD
1840   ASSIGN #ALPHA-MTH = #CLOSE-MMM
1850   ASSIGN #NUM-YR = #CLOSE-YY
1860   ASSIGN #ACTION = 'CAL'
1870   PERFORM CONVERT
1880   ASSIGN #CLOSE-PARS = #PARS
1890 ELSE
1900   IF #RUN-NBR NE ' '
1910     THEN
1920     REINPUT 'CASE CLOSE DATE REQUIRED'
1930       MARK *#CLOSE-DATE SOUND ALARM
1940   END-IF /* (1900)
1950 END-IF /* (1690)
1960 **
1970 IF #DEST = ' '
1980   THEN
1990   REINPUT 'PRINTER ADDRESS REQUIRED'
2000     MARK *#DEST SOUND ALARM
2010 END-IF /* (1970)
2020 **
2030 IF #DEST EQ MASK (...' ')                        /* SUPPLY LEADING ZERO
2040   THEN                                           /* TO 3-BYTE ADDRESS
2050   COMPRESS '0' #DEST-SV INTO #DEST
2060     LEAVING NO
2070 END-IF /* (2030)
2080 **
2090 IF #DEST NE MASK (9999)
2100   THEN
2110   REINPUT 'INVALID PRINTER ADDRESS'
2120     MARK *#DEST SOUND ALARM
2130 END-IF /* (2090)
2140 **
2150 ASSIGN #GETCHR-TID = #DEST-N                     /* PRINTER ADDRESS
2160 ASSIGN #LENGTH = 16                              /* AUDIT
2170 CALL 'GETCHR' #GETCHR-RET-CODE #GETCHR-FIELD #LENGTH
2180   #GETCHR-TID
2190 IF #GETCHR-RET-CODE NE H'00' OR #MAX-NBR-LINES NE H'00'
2200   THEN
2210   REINPUT 'NO MATCH PRINTER ADDRESS'
2220     MARK *#DEST SOUND ALARM
2230 END-IF /* (2190)
2240 **
2250 IF #AWD-PARS NE 0
2260   THEN
2270   HISTOGRAM CONTROL-HIST1 AWARD-DATE
2280     STARTING WITH #AWD-PARS THRU #AWD-PARS
2290   END-HISTOGRAM /* (2270)
2300   IF *COUNTER (2270) EQ 0
2310     THEN
2320     REINPUT 'SPECIFIED AWARD DATE NOT FOUND IN PROCESS CONTROL FILE'
2330       MARK *#AWD-DATE SOUND ALARM
2340   END-IF /* (2300)
2350   FIND (1) CONTROL WITH AWARD-DATE = #AWD-PARS
2360     ASSIGN #SUPER-CLOSE = CLOSE-DATE (2350)
2370   END-FIND /* (2270)
2380 ELSE
2390   HISTOGRAM CONTROL-HIST2 CLOSE-DATE
2400     STARTING WITH #CLOSE-PARS THRU #CLOSE-PARS
2410   END-HISTOGRAM /* (2390)
2420   IF *COUNTER (2390) EQ 0
2430     THEN
2440     REINPUT 'NO PROCESS CONTROL RECORD FOUND FOR SPECIFIED CLOSE DATE'
2450       MARK *#CLOSE-DATE SOUND ALARM
2460   END-IF /* (2420)
2470   FIND (1) CONTROL WITH CLOSE-DATE = #CLOSE-PARS
2480     FOR #I = 1 THRU C*RUN-NUMBER-CROSS-REFERENCE
2490       IF RUN-NBR (#I) = #RUN-NBR-NUM
2500         THEN
2510         ASSIGN #SUPER-CLOSE = RUN-NBR-PSEUDO-DATE (#I)
2520       END-IF /* (2490)
2530     END-FOR /* (2480)
2540   END-FIND /* (2470)
2550   IF #SUPER-CLOSE = 0
2560     THEN
2570     REINPUT 'SPECIFIED RUN NUMBER NOT FOUND IN PROCESS CONTROL RECORD'
2580     MARK *#RUN-NBR SOUND ALARM
2590   END-IF /* (2550)
2600 END-IF /* (2250)
2610 **
2620 ** AUDIT MATRIX TYPE AND SELECT RELEVANT PROG    -     SES 01/00
2630 **
2640 IF NOT (#MATRIX-TYPE = 'C' OR = 'E')
2650   REINPUT 'PLEASE INDICATE MATRIX TYPE WITH A "C" OR "E"'
2660   MARK *#MATRIX-TYPE ALARM
2670 END-IF
2680 IF #MATRIX-TYPE = 'E'
2690   #PROG := 'DCU05B10'
2700 ELSE
2710   #PROG := 'DCU05B15'
2720 END-IF
2730 ***********************************************************************
2740 *****                ******   BEGIN JCL   *****                   *****
2750 ***********************************************************************
2760 **
2770 ASSIGN #L1 =
2780   '//$EA0003M JOB ,''BAGWELL RM214'',CLASS=>,MSGLEVEL=(1,0),MSGCLASS=&'
2790 ASSIGN #L2 =
2800   '//STEPA  EXEC  CSBATCH,PARM=''PRINTER=(    )'',SYSOUT=%,STATUS=*'
2810 ASSIGN #PRINTER = #DEST
2820 CALL 'WHERE' #W #RC
2830 IF #RC NE '0'
2840   THEN
2850   WRITE 'ERROR IN WHERE UTILITY - NOTIFY PROGRAMMING '/
2860     'HIT ENTER KEY TO RETURN TO PREVIOUS MENU'
2870   FETCH 'RAV'
2880 END-IF /* (2830)
2890 **
2900 ***********************************************************************
2910 *****              *****   SET JCL PARAMETERS   *****             *****
2920 ***********************************************************************
2930 **
2940 IF #W = 'T'
2950   THEN
2960   EXAMINE #L1 FOR '$' AND REPLACE WITH 'T'             /* MUKUNDA 12/24
2970   EXAMINE #L1 FOR '>' AND REPLACE WITH 'X'
2980   EXAMINE #L1 FOR '&' AND REPLACE WITH 'X'
2990   EXAMINE #L2 FOR '%' AND REPLACE WITH 'X'
3000   EXAMINE #L2 FOR '*' AND REPLACE WITH 'TEST'
3010 END-IF /* (2940)
3020 **
3030 IF #W = 'A'
3040   THEN
3050   EXAMINE #L1 FOR '$' AND REPLACE WITH 'A'             /* MUKUNDA 12/24
3060   EXAMINE #L1 FOR '>' AND REPLACE WITH 'X'
3070   EXAMINE #L1 FOR '&' AND REPLACE WITH 'Q'
3080   EXAMINE #L2 FOR '%' AND REPLACE WITH 'Q'
3090   EXAMINE #L2 FOR '*' AND REPLACE WITH 'CSAS'
3100 END-IF /* (3030)
3110 **
3120 IF #W = 'P'
3130   THEN
3140   EXAMINE #L1 FOR '$' AND REPLACE WITH 'U'              /* MUKUNDA 12/24
3150   EXAMINE #L1 FOR '>' AND REPLACE WITH 'Y'
3160   EXAMINE #L1 FOR '&' AND REPLACE WITH 'Q'
3170   EXAMINE #L2 FOR '%' AND REPLACE WITH 'Q'
3180   EXAMINE #L2 FOR '*' AND REPLACE WITH 'PROD'
3190 END-IF /* (3120)
3200 **
3210 **
3220 ASSIGN #L3 = '//SYSIN DD *'
3230 ASSIGN #L4 = 'RAPIDX,EA0003M'
3240 ASSIGN #L5 = 'EA0003M'
3250 COMPRESS #PROG #TYPE #DATE #SUPER-CLOSE #RUN-NBR INTO #L6
3260 ASSIGN #L7 = 'FIN'
3270 **FORMAT LS=82
3280 **WRITE
3290 **#L1
3300 **/ #L2
3310 **/ #L3
3320 **/ #L4
3330 **/ #L5
3340 **/ #L6
3350 **/ #L7
3360 **STOP
3370 ASSIGN #LENGTH = 560
3380 **
3390 FOR #D = 1 TO 5
3400   CALL 'RJE' #RTN-CODE #L1 #LENGTH
3410   IF #RTN-CODE = 0
3420     THEN
3430     ESCAPE BOTTOM (3390)
3440   END-IF /* (3410)
3450   IF #RTN-CODE = 4
3460     THEN
3470     ASSIGN #TIME = 1
3480     CALL 'ROLBCH' #TIME
3490   ELSE
3500     WRITE /// 'UNABLE TO PROCESS - CONTACT INFORMATION SERVICES'
3510       (I) 5X 'RETURN CODE:' #RTN-CODE
3520     STOP
3530   END-IF /* (3450)
3540 END-FOR /* (3390)
3550 **
3560 IF #RTN-CODE = 4
3570   THEN
3580   WRITE /// 'INPUT QUEUE FULL - RESUBMIT LATER'
3590   RELEASE VARIABLES
3600   FETCH 'MUKUNDASMH'
3610   STOP
3620 END-IF /* (3560)
3630 **
3640 WRITE //// 26T 'DOCTOR OPERATIONAL MATRIX REPORT'
3650   // 28T 'HAS BEEN ROUTED TO PRINTER'
3660   // 32T '(JOB NAME = UEA0003M)'
3670   //////////// 21T 'HIT ENTER KEY TO RETURN TO PREVIOUS MENU'
3680 RELEASE VARIABLES
3690 FETCH 'MUKUNDASMH'
3700 STOP
3710 **
3720 DEFINE SUBROUTINE CONVERT
3730 **
3740 CALL 'RV008' #WORK
3750 IF #RET-CODE NE '0'
3760   THEN
3770   REINPUT 'DATE CONVERSION ERROR - CONTACT INFORMATION SERVICES'
3780 END-IF /* (3750)
3790 **
3800 END-SUBROUTINE /* (3720)
3810 **
3820 END
***** End of List *****
