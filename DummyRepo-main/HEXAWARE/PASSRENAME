1NEXT L  PASS
0350 **
0360 SET KEY PF1 PF2 PF3 PF4 PF5 PF6 PF7 PF8 PF9 PF10 PF11 PF12
0370 **
0380 RESET #MENU (A1) #EMP-FLD (A7) #DATE-D (D)
0390   #RELEASE (N5) #PARS-BEGIN (N5) #K (P3) #L (P3) #SENDING-PGM (A8)
0400   #ASSIGN-SUPER (A15)
0410 *
0420 REDEFINE
0430   #ASSIGN-SUPER (#EMP-NBR (A7) #ASSIGN-DATE (A8))
0440   #EMP-NBR (#SAV1 (A6) #SAV2 (A1))
0450   #EMP-FLD (#FLD1 (N1) #FLD2 (A6))
0460 ****
0470 FORMAT PS=55 IP=OFF
0480 ***
0490 INPUT #EMP-NBR #SENDING-PGM
0500 ***
0510 INPUT USING MAP 'PASSMAP1'
0520 ****
0530 IF *PF-KEY = 'PF1'
0540 THEN FETCH 'MAIN'
0550 IF *PF-KEY = 'PF2'
0560 IF #SENDING-PGM  = 'MUKUNDAM'
0570   THEN FETCH 'MUKUNDAM'
0580 ELSE
0590   FETCH 'PILMAINA'
0600 #USER-ID(A8) = *USER
0610 **
0620 IF #SAV1 NE MASK (999999)
0630 THEN REINPUT 'INVALID EMPLOYEE NUMBER'
0640   MARK 1 SOUND ALARM
0650 IF #SAV2 NE ' '
0660 THEN DO
0670   IF #SAV2 NE MASK (9)
0680   THEN REINPUT 'INVALID EMPLOYEE NUMBER'
0690     MARK 1 SOUND ALARM
0700 DOEND /*(0660)
0710 ELSE DO
0720   #FLD1 = 0
0730   #FLD2 = #SAV1
0740   #EMP-NBR = #EMP-FLD
0750 DOEND /*(0710)
0760 **
0770 **********
0780 READ (1) DOCTOR-PERSONNEL-DATA WITH EMPLOYEE-NBR EQ #EMP-NBR /* 04/92
0790   IF EMPLOYEE-NBR NE #EMP-NBR                /*************************
0800   THEN REINPUT 'INVALID EMPLOYEE NUMBER'   /*  ADDED 04/92 - BJF
0810     MARK 1 SOUND ALARM                       /*************************
0820  HISTOGRAM (1) DOCTOR-ASSIGNMENT-DATA EMPLOYEE-NBR  /********************
0830       STARTING FROM #EMP-NBR THRU #EMP-NBR             /*
0840   LOOP (0820)                                       /*  THIS SECTION
0850   IF *COUNTER (0820) = 0                            /*     MOVED
0860   THEN DO                                         /*    04/92 - BJF
0870     EJECT                                         /*
0880 **                                                /*
0890     WRITE //// 30T 'NO DATA PRESENT'                  /*
0900       //   30T 'END OF DISPLAY'                   /*
0910      RESET #EMP-NBR
0920     FETCH 'PASS' #EMP-NBR #SENDING-PGM                           /*
0930   DOEND /*(0860)                                  /********************
0940 ******************** DEAN **********************************************
0950   COMPRESS LAST-NAME (0780) ',' INTO #NAME (A21)
0960     LEAVING NO SPACE
0970   COMPRESS #NAME FIRST-NAME (0780) MIDDLE-NAME (0780)
0980     INTO #COMPRESS (A50)
0990   #DATE-D := D'2024-12-30' + RETIREMENT-DATE
1000   MOVE EDITED #DATE-D(EM=YYMMDD) TO #RETIRE (A6)
1010   MOVE EDITED #DATE-D (EM=YYYYMMDD) TO #ASSIGN-DATE
1020   #TERMINATE-DTE (N5) = TERMINATION-DATE (0780)    /* SAVE TERM DTE
1030 LOOP (0780)
1040 WRITE 01T 'EStaff REPORT BOX:'
1050       19T 'DISPLAY ASSIGNMENT INFORMATION'
1060       60T 'ID#: 04-20,01-23,***'
1070   // 22T #EMP-NBR #COMPRESS
1080   // 'ASSIGNMENT DATA'
1090   / '---------------'
1100   /
1110   'BASE       EQUIP/ADM        POSITION      DEPARTMENT        BEGIN'
1120   '      END'
1130   /
1140   '----       ---------        --------      ----------       -------'
1150   '   ------'
1160 ************************************************************************
1170 **THE FOLLOWING CODE WILL CHECK THE RELEASE DATE AND WILL NOT
1180 **DISPLAY ANY ASSIGNMENTS THAT ARE FUTURE.
1190 ************************************************************************
1200 READ CSS-CONTROL-RECORD                                       /*LAP
1210   MOVE ASSIGNMENT-RELEASE-DATE TO #RELEASE                    /*LAP
1220   ESCAPE BOTTOM                                               /*LAP
1230 LOOP /*(1200)                                                 /*LAP
1240 *
1250 READ DOCTOR-ASSIGNMENT-DATA IN DESCENDING SEQUENCE BY
1260   EMPNBR-YEAR-PREFIX-END-DATE STARTING FROM #ASSIGN-SUPER
1270   IF EMPLOYEE-NBR NE #EMP-NBR ESCAPE BOTTOM
1280   RESET #START (A7) #END (A7)
1290 *
1300   MOVE EDITED BEGIN-DATE (1250) TO #DATE-D (EM=YYMMDD)
1310   MOVE EDITED #DATE-D (EM=DDLLLYY) TO #START
1320 *
1330   #PARS-BEGIN := #DATE-D - D'2024-12-30'                         /*LAP
1340 *
1350   IF #RELEASE NE 0 THEN DO
1360     IF #PARS-BEGIN >= #RELEASE THEN DO                             /*LAP
1370       RESET #NOSHOW (A1)                                           /*LAP
1380       MOVE 'Y' TO #NOSHOW                                          /*LAP
1390       ESCAPE TOP                                                   /*LAP
1400     DOEND /*(1360)
1410   DOEND /*(1350)                                                 /*LAP
1420 *
1430   IF END-DATE (1250) NE #RETIRE OR #TERMINATE-DTE NE 0  /* END-DATE OF
1440   THEN DO                                             /* CURRENT ASSGNMT
1450     MOVE EDITED END-DATE TO #DATE-D (EM=YYMMDD)       /* TO BE DISPLAYED
1460     MOVE EDITED #DATE-D (EM=DDLLLYY) TO #END          /* AS BLANK; ELSE
1470   DOEND /*(1440)                                      /* DISPLAY DATE
1480 **
1490   IF #NOSHOW = 'Y' MOVE ' ' TO #END                             /*LAP
1500   RESET #NOSHOW                                                 /*LAP
1510 **
1520   WRITE
1530    BASE-CODE 11X EQUIP-ADMIN-TYPE 14X POSITION 13X DEPARTMENT 11X #START
1540     4X #END
1550 LOOP /* (1250) - ASSIGNMENT FILE
1560 **
1570 **  DISPLAY LEAVE OF ABSENCE DATA
1580 **
1590 RESET #STATUS-WORK (A23) #STATUS-ENTRY-AREA (A69)
1600   #STATUS-WORK2 (A23) #I (N2) #J (N1) #LEAVE-IND (A1)
1610 **
1620 REDEFINE #STATUS-ENTRY-AREA (#STATUS-ENTRY (A23/3))       /* 1/91-RAV
1630   #STATUS-WORK (#STATUS (A4) #LEAVE-BEGIN (A7) #LEAVE-END (A7)
1640   #NUM-BEGIN (N5))
1650   #STATUS-WORK2 (18X #NUM-BEGIN2 (N5))
1660 **
1670 READ DOCTOR-VTS-FILE BY EMPNBR-BEGIN STARTING FROM #EMP-NBR
1680   IF EMPLOYEE-NBR NE #EMP-NBR THEN ESCAPE
1690   RESET #LEAVE-IND
1700   #STATUS = SCHEDULE-CODE (1670)
1710   FIND (1) DOCTOR-SCHEDULE-CODE WITH SCHEDULE-CODE EQ #STATUS
1720     IF LEAVE-OF-ABSENCE-IND (1710) = 'Y'
1730     THEN #LEAVE-IND = 'Y'
1740   LOOP (1710)
1750 **
1760   IF #LEAVE-IND = 'Y'
1770   THEN DO
1780     #DATE-D := D'2024-12-30' + LEAVE-BEGIN-DATE (1670)
1790     MOVE EDITED #DATE-D (EM=DDLLLYY) TO #LEAVE-BEGIN
1800     #NUM-BEGIN = LEAVE-BEGIN-DATE (1670)
1810 ***
1820     IF LEAVE-END-DATE (1670) NE 0
1830     THEN DO
1840       #DATE-D := D'2024-12-30' + LEAVE-END-DATE (1670)
1850       MOVE EDITED #DATE-D (EM=DDLLLYY) TO #LEAVE-END
1860     DOEND /*(1830)
1870     #I = #I + 1
1880     MOVE #STATUS-WORK TO #STATUS-ENTRY (#I)                 /* 1/91-RAV
1890     IF #I = 3 THEN RESET #I
1900   DOEND /*(1770)
1910 LOOP (1670)
1920 **
1930 IF #STATUS-ENTRY-AREA NE ' '
1940 THEN DO
1950   FOR #I = 1 TO 3
1960     MOVE #STATUS-ENTRY (#I) TO #STATUS-WORK                /* 1/91-RAV
1970     #J = #I + 1
1980     IF #J > 3 THEN ESCAPE
1990     MOVE #STATUS-ENTRY (#J) TO #STATUS-WORK2               /* 1/91-RAV
2000     IF #NUM-BEGIN < #NUM-BEGIN2
2010     THEN DO
2020       MOVE #STATUS-WORK2 TO #STATUS-ENTRY (#I)             /* 1/91-
2030       MOVE #STATUS-WORK TO #STATUS-ENTRY (#J)              /* RAV
2040     DOEND /*(2010)
2050   LOOP (1950)
2060 ***********
2070   WRITE / 4X 'LEAVE OF ABSENCE DATA'
2080     / 4X '---------------------'
2090   FOR #I = 1 TO 3
2100     MOVE #STATUS-ENTRY (#I) TO #STATUS-WORK                /* 1/91-RAV
2110     IF #STATUS-WORK NE ' '
2120     THEN WRITE 'STATUS:' #STATUS 4X 'BEGIN DATE:' #LEAVE-BEGIN 4X
2130       'END DATE:' #LEAVE-END
2140   LOOP (2090)
2150 DOEND /*(1940)
2160 SKIP 1
2170 *****
2180 **  WRITE 10T 'END OF DISPLAY - HIT ENTER TO RETURN TO PREVIOUS MENU'
2190 WRITE 30T 'END OF DISPLAY'
2200 #K := *LINE-COUNT
2210 DIVIDE 23 INTO #K REMAINDER #L
2220 #L := 24 - #L
2230 IF #L = 23 OR = 24
2240   IGNORE
2250 ELSE DO
2260   FOR #I 1 TO #L
2270     WRITE '  '
2280   LOOP (2260)
2290 DOEND /* (2250)
2300 RESET #EMP-NBR
2310 FETCH 'PASS' #EMP-NBR #SENDING-PGM
2320 **
2330 END
***** End of List *****
