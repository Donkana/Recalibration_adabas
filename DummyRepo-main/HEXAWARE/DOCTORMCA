1NEXT L  DOCTORMCA

0170 DEFINE DATA LOCAL
0180   1 #DATE-D (D)
0190   1 #CONV-DATE (D)    CONST<D'2024-12-30'>
0200   1 #TODAY (N5)
0210   1 #FUTURE-35 (N5)
0220   1 #EMP7 (A7)
0230   1 #OPT (A1)
0240   1 #SENDING-PGM (A8)
0250   1 #CASE-AWARD-SUPER (A14)
0260   1 REDEFINE #CASE-AWARD-SUPER
0270     2 #CASE-FILE-TYPE (A1)
0280     2 #CASE-DATE (N5)
0290     2 #CASE-AWARD-BASE (A3)
0300     2 #CASE-AWARD-EQUIP (A3)
0310   1 CASE-AWARD-STATUS VIEW OF DOCTOR-CASE-AWARD-STATUS
0320     2 CASE-FILE-TYPE
0330     2 BEGIN-DATE
0340     2 CAPT-SIL-LOCKOUT-IND
0350     2 FIRST-SIL-LOCKOUT-IND
0360     2 SECOND-SIL-LOCKOUT-IND
0370   1 #ASSIGN-SUPER (A15)
0380   1 REDEFINE #ASSIGN-SUPER
0390     2 #ASSIGN-EMP (A7)
0400     2 #ASSIGN-DATE (A8)
0410     2 REDEFINE #ASSIGN-DATE
0420       3 #ASSIGN-YY (A2)
0430       3 #ASSIGN-YYMMDD (A6)
0440   1 ASSIGNMENT VIEW OF DOCTOR-ASSIGNMENT-DATA
0450     2 EMPLOYEE-NBR
0460     2 BASE-CODE
0470     2 EQUIP-ADMIN-TYPE
0480     2 POSITION
0490     2 YEAR-PREFIX                  /* MUKUNDA 2/18
0500     2 END-DATE                     /* MUKUNDA 2/18
0510   1 CASE-PERIOD-DATES VIEW OF DOCTOR-CASE-PERIOD-DATES
0520     2 CURRENT-BEGIN-DATE
0530     2 FUTURE-BEGIN-DATE
0540   1 #CASES-LOCKED (A1)
0550 *
0560 * FOLLOWING FIELDS ADDED FOR VIRTUAL BASE OPTION - MUKUNDA 2/18
0570   1 #CASE-PERIOD (A7)
0580   1 #CASE-PERIOD-A8 (A8)
0590   1 #CASE-PARS (N5)
0600   1 #CATEGORY-INFORMATION (A36)
0610   1 REDEFINE #CATEGORY-INFORMATION
0620     2 #CATEGORY (A7)
0630     2 REDEFINE #CATEGORY
0640       3 #BASE (A3)
0650       3 #EQUIP (A3)
0660       3 #POSITION (A1)
0670   1 #POSTED-IND (A1)
0680   1 #I (I4)
0690   1 CATEGORY-CONTROL VIEW OF DOCTOR-VACANCY-CATEGORY-CONTROL
0700     2 CLOSE-DATE
0710     2 RECORD-STATUS
0720     2 C*CATEGORY-INFORMATION
0730     2 CATEGORY-INFORMATION (1:10)
0740   1 #ASSIGN-CATEGORY (A7)
0750   1 REDEFINE #ASSIGN-CATEGORY
0760     2 #ASSIGN-BASE (A3)
0770     2 #ASSIGN-EQUIP (A3)
0780     2 #ASSIGN-POS (A1)
0790 END-DEFINE
0800 **
0810 RELEASE STACK
0820 RELEASE VARIABLES
0830 **
0840 SET KEY PF1 PF2
0850 *
0860 COMPRESS '0' *USER INTO #EMP7 LEAVING NO                   /* DVM 12/24
0870 INPUT USING MAP 'PILMCA' #OPT
0880 IF *PF-KEY = 'PF1' THEN FETCH 'MAIN' END-IF
0890 IF *PF-KEY NE 'ENTR' THEN REINPUT 'INVALID PF KEY'  /* 2/91  BRS
0900 END-IF
0910 *
0920 **
0930 IF #OPT = 'A' THEN                    /* RELATIVE RANKING.  DVM 12/24
0940   ASSIGN #SENDING-PGM = 'DOCTORMCA'
0950   FETCH 'RANK' #EMP7 #SENDING-PGM
0960 END-IF
0970 *
0980 IF #OPT = 'B' THEN
0990   READ (1) CASE-PERIOD-DATES
1000     #DATE-D := #CONV-DATE + FUTURE-BEGIN-DATE
1010     MOVE EDITED #DATE-D (EM=YYYYMMDD) TO #ASSIGN-DATE
1020     #CASE-DATE := CURRENT-BEGIN-DATE
1030     #FUTURE-35 := FUTURE-BEGIN-DATE - 35
1040   END-READ /* (0990)
1050   #CASE-FILE-TYPE := 'I'
1060 *
1070 * IF SYSTEM DATE/TIME IS AFTER 0700 OF THE 35TH DAY BEFORE THE FUTURE
1080 * CASE BEGIN DATE, MAKE SURE ALL CASES ARE LOCKED.
1090 *
1100   #TODAY := *DATX - #CONV-DATE
1110   IF #TODAY GT #FUTURE-35 OR (#TODAY = #FUTURE-35 AND *TIMN GE 0700000)
1120     READ CASE-AWARD-STATUS WITH TYPE-DATE-BASE-EQUIP = #CASE-AWARD-SUPER
1130       IF CASE-FILE-TYPE NE 'I' OR BEGIN-DATE NE #CASE-DATE THEN
1140         ESCAPE BOTTOM
1150       END-IF /* (1130)
1160       IF CAPT-SIL-LOCKOUT-IND = 'N' OR FIRST-SIL-LOCKOUT-IND = 'N'
1170         OR SECOND-SIL-LOCKOUT-IND = 'N' THEN
1180         GET CASE-AWARD-STATUS *ISN(1120)
1190         RESET CAPT-SIL-LOCKOUT-IND FIRST-SIL-LOCKOUT-IND
1200               SECOND-SIL-LOCKOUT-IND
1210         UPDATE (1180)
1220       END-IF /* (1160)
1230     END-READ /* (1120)
1240     END TRANSACTION
1250     REINPUT 'SIL CASES UNAVAILABLE AT THIS TIME'
1260   END-IF /* (1110)
1270 *
1280 * NOT AFTER 0700 THE 35TH DAY BEFORE FUTURE CASE PERIOD, SO CHECK TO SEE
1290 * IF CASES LOCKED FOR THIS DOCTOR'S CATEGORY (NO SILS POSTED).
1300 *
1310   ASSIGN #ASSIGN-EMP = #EMP7
1320   READ (1) ASSIGNMENT BY EMPNBR-YEAR-PREFIX-END-DATE STARTING FROM
1330     #ASSIGN-SUPER
1340     IF EMPLOYEE-NBR NE #EMP7 THEN
1350       REINPUT 'NO ASSIGNMENT FOR INPUT EMPLOYEE NBR' SOUND ALARM
1360     END-IF /* (1340)
1370     #CASE-AWARD-BASE := BASE-CODE
1380     #CASE-AWARD-EQUIP := EQUIP-ADMIN-TYPE
1390     FIND CASE-AWARD-STATUS WITH TYPE-DATE-BASE-EQUIP = #CASE-AWARD-SUPER
1400       IF (POSITION = 'A' AND CAPT-SIL-LOCKOUT-IND = 'N') OR
1410         (POSITION = 'B' AND FIRST-SIL-LOCKOUT-IND = 'N') OR
1420         (POSITION = 'C' AND SECOND-SIL-LOCKOUT-IND = 'N') THEN
1430         #CASES-LOCKED := 'N'
1440       END-IF /* (1400)
1450     END-FIND /* (1390)
1460     IF #CASES-LOCKED NE 'N'      /* CASES ARE LOCKED
1470       REINPUT 'SIL CASES UNAVAILABLE FOR YOUR CATEGORY AT THIS TIME'
1480     END-IF /* (1460)
1490   END-READ /* (1320)   - ASSIGNMENT
1500   FETCH 'SILCASE'
1510 END-IF
1520 *
1530 IF #OPT = 'C' THEN FETCH 'GLDNCASES' END-IF   /* PBS GOLDEN DAY CASES
1540 *                                            /* PJG 02/09
1550 IF #OPT = 'D'
1560   READ (1) CASE-PERIOD-DATES
1570     #CASE-PARS := FUTURE-BEGIN-DATE
1580     #DATE-D := #CONV-DATE + FUTURE-BEGIN-DATE
1590     MOVE EDITED #DATE-D (EM=YYYYMMDD) TO #CASE-PERIOD-A8
1600     MOVE EDITED #DATE-D (EM=DDLLLYY) TO #CASE-PERIOD
1610   END-READ /* (0990)
1620 *
1630   #ASSIGN-EMP := #EMP7
1640   MOVE EDITED *DATX (EM=YYYYMMDD) TO #ASSIGN-DATE
1650   READ ASSIGNMENT BY EMPNBR-YEAR-PREFIX-END-DATE
1660     STARTING FROM #ASSIGN-SUPER
1670     IF EMPLOYEE-NBR(1650) NE #ASSIGN-EMP
1680       ESCAPE BOTTOM
1690     END-IF /* (1830)
1700     IF *COUNTER(1650) = 1      /* 1ST RECORD, CURRENT ASSIGNMENT
1710       #ASSIGN-EQUIP  := EQUIP-ADMIN-TYPE
1720       #ASSIGN-POS    := POSITION
1730     END-IF /* (1700)
1740     #ASSIGN-YY     := YEAR-PREFIX
1750     #ASSIGN-YYMMDD := END-DATE
1760     IF #ASSIGN-DATE GT #CASE-PERIOD-A8  /* ASSIGNMENT DURING INPUT CASE PD
1770       IF (EQUIP-ADMIN-TYPE NE #ASSIGN-EQUIP OR
1780         POSITION NE #ASSIGN-POS)
1790         REINPUT 'DOCTOR MUST BE QUALIFIED ON:1::2: TO ENTER CASES',
1800             EQUIP-ADMIN-TYPE,POSITION ALARM
1810       END-IF /* (1770)
1820       #ASSIGN-BASE := BASE-CODE
1830       ESCAPE BOTTOM
1840     END-IF /* (1760)
1850   END-READ /* (1650) - ASSIGNMENT FILE
1860 *
1870   FIND CATEGORY-CONTROL WITH CLOSE-DATE = #CASE-PARS
1880     IF NOT(RECORD-STATUS = 'T' OR = 'U' OR = 'V')
1890       ESCAPE TOP
1900     END-IF /* (1870)
1910     IF RECORD-STATUS = 'T'
1920       REINPUT 'CASES LOCKED'
1930     END-IF /* (1910)
1940     FOR #I = 1 TO C*CATEGORY-INFORMATION
1950       #CATEGORY-INFORMATION := CATEGORY-INFORMATION (#I)
1960       IF #EQUIP = #ASSIGN-EQUIP AND #POSITION = #ASSIGN-POS
1970         AND #BASE NE #ASSIGN-BASE
1980         #POSTED-IND := 'Y'                             /* VALID BASE FOR
1990         ESCAPE BOTTOM                                  /* DOCTOR TO CASE
2000       END-IF /* (1960)
2010     END-FOR /* (1940)
2020   END-FIND /* (1870) - CAT CONTROL RECORD
2030   IF #POSTED-IND NE 'Y'
2040     REINPUT
2050      'NO VIRTUAL BASES OR TEMPORARY DUTIES POSTED FOR:1::2:',
2060         #ASSIGN-EQUIP,#ASSIGN-POS ALARM
2070   END-IF /* (2030)
2080   FETCH 'VIRTCASEP' #EMP7 #CASE-PARS #CASE-PERIOD #CASE-PERIOD-A8
2090 END-IF /* (1550) - OPTION 'D' - VIRTUAL CASES - MUKUNDA 2/18
2100 *                                            /* PJG 02/09
2110 REINPUT 'INVALID OPTION' MARK 1 SOUND ALARM
2120 END
***** End of List *****
